buildscript {
    repositories {
        maven {
            url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
        }
    }
}

plugins {
    id "com.diffplug.spotless" version "6.25.0"
    id "org.sonarqube" version "5.1.0.4882"
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'

group 'uk.gov.di'

repositories {
    maven {
        url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
    }
}

ext {
    dependencyVersions = [
        log4j: "2.24.3"
    ]
}

dependencies {
    constraints {
        implementation("org.eclipse.jetty:jetty-server:[10.0.23,11)")
        implementation("org.eclipse.jetty:jetty-webapp:[10.0.23,11)")
        implementation("io.netty:netty-common:[4.1.115.Final,4.2)")
    }

    implementation(platform("software.amazon.awssdk:bom:2.30.21"))
    implementation "com.sparkjava:spark-core:2.9.4",
            "com.sparkjava:spark-template-mustache:2.7.1",
            "com.nimbusds:oauth2-oidc-sdk:11.20",
            "org.apache.logging.log4j:log4j-api:${dependencyVersions.log4j}",
            "org.apache.logging.log4j:log4j-core:${dependencyVersions.log4j}",
            "org.apache.logging.log4j:log4j-slf4j2-impl:${dependencyVersions.log4j}",
            "org.apache.httpcomponents:httpclient:4.5.14",
            'net.minidev:json-smart:2.5.2',
            "com.google.code.gson:gson:2.13.1",
            "software.amazon.awssdk:secretsmanager",
            'com.fasterxml.jackson.core:jackson-databind:2.19.0',
            "com.nimbusds:nimbus-jose-jwt:9.41.2"
    implementation("decentralized-identity:did-common-java:1.14.0")
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.12.2',
            'org.mockito:mockito-inline:5.2.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.12.2',
            'org.junit.platform:junit-platform-launcher'
}

spotless {
    java {
        target "**/*.java"
        googleJavaFormat("1.13.0").aosp()
        importOrder "", "javax", "java", "\\#"
    }

    groovyGradle {
        target "**/*.gradle"
        greclipse().configFile("tools/spotless-gradle.properties")
    }
}

sonar {
    properties {
        property "sonar.projectKey", "govuk-one-login_relying-party-stub"
        property "sonar.organization", "govuk-one-login"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

run {
    debugOptions {
        enabled = true
        port = 8083
        server = true
        suspend = false
    }
    file('.env').exists() && file('.env').readLines().each() {
        if (!it.isEmpty() && !it.startsWith('#') && it.contains('=')) {
            def (key, value) = it.tokenize('=')
            environment key, value
        }
    }
}

mainClassName = 'uk.gov.di.App'
